
AC_PREREQ(2.53)
AC_INIT(avg, 0.2, coder@c-base.org)
MAJOR_VERSION=0
MINOR_VERSION=2
MICRO_VERSION=0
VERSION=$MAJOR_VERSION.$MINOR_VERSION.$MICRO_VERSION dnl

AC_SUBST(VERSION)
AC_CONFIG_SRCDIR([src/avgconfig.h.in])
AM_INIT_AUTOMAKE(avg, $VERSION)
# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP

AC_DISABLE_STATIC
AC_PROG_LIBTOOL

AC_CHECK_PROG(xml2_installed, [xml2-config], "yes")
if test -z "$xml2_installed"
then
    AC_MSG_ERROR([libxml2 not found. Aborting.])
fi

PKG_CHECK_MODULES([pango], [pango])

AC_CHECK_PROG(magick_installed, [Magick++-config], "yes")
if test -z "$magick_installed"
then
    AC_MSG_ERROR([ImageMagick not found. Aborting.])
fi

AC_CHECK_LIB(avformat, main, libavformat_installed=yes, , -lavcodec )
if test -z "$libavformat_installed"
then
    AC_MSG_ERROR([libavformat (ffmpeg) not found. Aborting.])
fi
AC_CHECK_LIB(avformat, main)
AC_CHECK_LIB(avcodec, main)
AC_CHECK_FUNC(av_seek_frame, libavformat_seek=yes)
if test -z "$libavformat_seek"
then
    AC_MSG_ERROR([libavformat (ffmpeg) does not support av_seek_frame. Aborting.])
fi

AM_PATH_PYTHON

AZ_PYTHON_PATH( ) 
AZ_PYTHON_VERSION_ENSURE( [2.3] )
AZ_PYTHON_CSPEC
AZ_PYTHON_LSPEC

AC_CHECK_LIB(boost_python, main, libboost_python_installed=yes, , $PYTHON_LSPEC)
if test -z "$libboost_python_installed"
then
    AC_MSG_ERROR([boost.python not found. Aborting.])
fi
    
AC_ARG_ENABLE(dc1394,
              AC_HELP_STRING([--enable-dc1394], 
                    [compile support for firewire cameras]),
                    enable_1394=$enableval, enable_1394=maybe)
if test "$enable_1394" = maybe; then
    AC_CHECK_LIB(dc1394_control, main, enable_1394=yes, enable_1394=no, -lraw1394)
fi
AM_CONDITIONAL(ENABLE_1394, test x$enable_1394 = xyes)
if test "$enable_1394" = yes; then
    AC_DEFINE(AVG_ENABLE_1394, 1, [Enable firewire camera support])
fi 

AC_ARG_ENABLE(DFB,
              AC_HELP_STRING([--enable-DFB], 
                    [compile support for DirectFB rendering backend]),
                    enable_DFB=$enableval, enable_DFB=maybe, )
if test "$enable_DFB" = maybe; then
    AC_CHECK_PROG(libdfb_config, directfb-config, yes)
    if test "$libdfb_config" = yes; then
        AC_CHECK_LIB(directfb, main, enable_DFB=yes, enable_DFB=false,
                    `directfb-config --libs`)
    fi
fi
AM_CONDITIONAL(ENABLE_DFB, test x$enable_DFB = xyes)
if test "$enable_DFB" = yes; then
    AC_DEFINE(AVG_ENABLE_DFB, 1, [Enable DirectFB rendering backend])
fi

AC_CHECK_HEADER([linux/ppdev.h], enable_ParPort=yes, enable_ParPort=no)
AM_CONDITIONAL(ENABLE_PARPORT, test x$enable_ParPort = xyes)
if test "$enable_ParPort" = yes; then
    AC_DEFINE(AVG_ENABLE_PARPORT, 1, [Enable parallel port support])
fi 

AX_CHECK_GL
AX_CHECK_GLU

AC_CHECK_PROG(sdl_installed, [sdl-config], "yes")
AM_CONDITIONAL(ENABLE_GL, [test x$no_GL != xyes] && [test x$sdl_installed = xyes])
if [test x$no_GL != xyes] && [test x$sdl_installed = xyes]; then
    AC_DEFINE(AVG_ENABLE_GL, 1, [Enable OpenGL rendering backend])
else
    AC_MSG_WARN([OpenGL rendering not enabled])
fi

AM_CONFIG_HEADER(src/avgconfig.h)
AC_OUTPUT(avg-config Makefile src/Makefile man/Makefile  
    src/base/Makefile src/conradrelais/Makefile src/python/Makefile 
    src/player/Makefile src/parport/Makefile src/graphics/Makefile)
