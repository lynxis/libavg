
AC_PREREQ(2.53)
AC_INIT(libavg, 0.1, coder@c-base.org)
AC_CONFIG_SRCDIR([src/AVGDFBDisplayEngine.h])
AM_INIT_AUTOMAKE(libavg, 0.1)

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
# AC_PROG_RANLIB  # static library support
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL

# Checks for libraries.

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
# AC_HEADER_STDBOOL
AC_C_CONST

# Checks for library functions.
AC_CHECK_FUNCS([strdup strtol])

AC_ARG_ENABLE(dc1394,
              AC_HELP_STRING([--enable-dc1394], 
                    [compile support for firewire cameras]),
                    enable_1394=$enableval, enable_1394=maybe)
if test "$enable_1394" = maybe; then
    AC_CHECK_LIB(dc1394_control, main, enable_1394=yes, enable_1394=no, -lraw1394)
fi
AM_CONDITIONAL(ENABLE_1394, test x$enable_1394 = xyes)
if test "$enable_1394" = yes; then
    AC_DEFINE(AVG_ENABLE_1394, 1, [Enable firewire camera support])
fi 

AC_ARG_ENABLE(DFB,
              AC_HELP_STRING([--enable-DFB], 
                    [compile support for DirectFB rendering backend]),
                    enable_DFB=$enableval, enable_DFB=maybe, )
if test "$enable_DFB" = maybe; then
    AC_CHECK_PROG(libdfb_config, directfb-config, yes)
    if test "$libdfb_config" = yes; then
        AC_CHECK_LIB(directfb, main, enable_DFB=yes, enable_DFB=false,
                    `directfb-config --libs`)
    fi
fi
AM_CONDITIONAL(ENABLE_DFB, test x$enable_DFB = xyes)
if test "$enable_DFB" = yes; then
    AC_DEFINE(AVG_ENABLE_DFB, 1, [Enable DirectFB rendering backend])
fi

AC_ARG_ENABLE(GL,
              AC_HELP_STRING([--enable-GL], 
                    [compile support for OpenGL rendering backend]),
                    enable_GL=$enableval, enable_GL=maybe)
if test "$enable_GL" = maybe; then
    AC_CHECK_LIB(GL, main, enable_GL=yes, enable_GL=no)
fi
AM_CONDITIONAL(ENABLE_GL, test x$enable_GL = xyes)
if test "$enable_GL" = yes; then
    AC_DEFINE(AVG_ENABLE_GL, 1, [Enable OpenGL rendering backend])
fi

AM_CONFIG_HEADER(src/avgconfig.h)
AC_OUTPUT(libavg-config Makefile src/Makefile man/Makefile)
